import java.util.jar.JarEntry
import java.util.jar.JarFile
import java.util.jar.JarOutputStream

group 'co.rsk'
version '1.2.1'

buildscript {
    repositories {
        jcenter()
    }
}

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java.srcDirs = ['src/test/java']
    }
    jmh {
        java.srcDirs = ['src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    testCompile group: 'com.google.guava', name: 'guava-testlib', version: '18.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    jmhCompile 'org.openjdk.jmh:jmh-core:1.22'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.22'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task generatePom(dependsOn: jar) {
    def jarName = jar.outputs.files.singleFile.name
    def pomFile = "${project.buildDir}/${libsDirName}/${jarName.take(jarName.lastIndexOf('.'))}.pom"
    ext.file = new File(pomFile)
    doLast {
        pom {
            project {
                groupId project.group
                artifactId project.name
                version project.version
                licenses {
                    license {
                        name 'GNU General Public License (GPL) version 3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo(pomFile)
    }
}

def stripJar(File originalFile) {
    def constantTimeForZipEntries = new GregorianCalendar(1980, Calendar.FEBRUARY, 1, 0, 0, 0).timeInMillis
    def reproducibleFile = File.createTempFile("temp", ".tmp")
    reproducibleFile.withOutputStream { fout ->
        def out = new JarOutputStream(fout)
        def jf = new JarFile(originalFile)
        jf.entries().unique {it.name}.sort {it.name}.each {
            def copy = new JarEntry(it.name)
            copy.time = constantTimeForZipEntries
            out.putNextEntry(copy)
            out << jf.getInputStream(it)
        }
        out.finish()
    }
    reproducibleFile.renameTo originalFile
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    main = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}


artifacts {
    archives jar
    archives(generatePom.file) {
        type 'text'
        builtBy generatePom
    }
    archives sourcesJar
    archives javadocJar
}

task buildProject(group: 'native libraries projcet build', description: 'builds libraries for supported OSs and packages them into productive jar') {
    dependsOn 'generateJniHeaders'
    dependsOn 'replaceJniHeaders'
    dependsOn 'buildAltbn128'
    dependsOn 'buildSecp256k1CurrentOS'
    finalizedBy 'build'
}

task buildProjectDev(group: 'native libraries projcet build', description: 'same as buildProject but uses experimental builds for each library instead') {
    dependsOn 'generateJniHeaders'
    dependsOn 'replaceJniHeaders'
    dependsOn 'buildAltbn128'
    dependsOn 'buildSecp256k1Cross'
    finalizedBy 'build'
}

// Enabling reproducibility

tasks.withType(Jar) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    doLast {
        stripJar(outputs.files.singleFile)
    }
}

// Altbn128 build tasks

task buildAltbn128(group: 'altbn128', description: 'builds altbn128 library with system go') {
    doLast {
        exec {
            workingDir 'altbn128'
            commandLine './buildAltbn128.sh'
        }

        delete 'src/main/resources/co/rsk/altbn128/cloudflare/native/linux/amd64/libbn128.so'
        copy {
            from 'altbn128/amd64/libbn128.so'
            into 'src/main/resources/co/rsk/altbn128/cloudflare/native/linux/amd64/'
        }

        delete 'src/main/resources/co/rsk/altbn128/cloudflare/native/linux/arm64/libbn128.so'
        copy {
            from 'altbn128/arm64/libbn128.so'
            into 'src/main/resources/co/rsk/altbn128/cloudflare/native/linux/arm64/'
        }
    }
}

// Secp256k1 build tasks

task buildSecp256k1CurrentOS(group: 'secp256k1', description: 'builds secp256k1 library for current OS (helpful for development)') {
    dependsOn 'cleanSecp256k1'
    dependsOn 'replaceJniHeaders'
    doLast {
        exec {
            workingDir 'secp256k1'
            commandLine './autogen.sh'
        }
        exec {
            workingDir 'secp256k1'
            commandLine './configure',
                    '--enable-experimental',
                    '--enable-module_ecdh',
                    '--enable-module-recovery',
                    '--enable-jni'
        }
        exec {
            workingDir 'secp256k1'
            commandLine 'make', 'clean'
        }
        exec {
            workingDir 'secp256k1'
            commandLine 'make'
        }
        copy {
            from 'secp256k1/.libs'
            include 'libsecp256k1.so.0.0.0'
            into 'src/main/resources/org/bitcoin/native/Linux/x86_64/'
            rename 'libsecp256k1.so.0.0.0', 'libsecp256k1.so'
        }
        copy {
            from 'secp256k1/.libs'
            include 'libsecp256k1.0.dylib'
            into 'src/main/resources/org/bitcoin/native/Mac/x86_64/'
            rename 'libsecp256k1.0.dylib', 'libsecp256k1.jnilib'
        }
        copy {
            from 'secp256k1/.libs'
            include 'libsecp256k1-0.dll'
            into 'src/main/resources/org/bitcoin/native/Windows/x86_64/'
            rename 'libsecp256k1-0.dll', 'secp256k1.dll'
        }
    }
}

task buildSecp256k1Cross(group: 'secp256k1', description: 'builds secp256k1 for Linux, Mac and Windows (only linux library supports reproducibility)') {
    dependsOn 'cleanSecp256k1'
    dependsOn 'replaceJniHeaders'
    doLast {
        exec {
            workingDir 'secp256k1'
            commandLine './build_linux.sh'
        }
        copy {
            from 'secp256k1/.libs/'
            include 'libsecp256k1.so.0.0.0'
            into 'src/main/resources/org/bitcoin/native/Linux/x86_64/'
            rename 'libsecp256k1.so.0.0.0', 'libsecp256k1.so'
        }
        exec {
            workingDir 'secp256k1'
            commandLine './build_mac.sh'
        }
        copy {
            from 'secp256k1/.libs/'
            include 'libsecp256k1.0.dylib'
            into 'src/main/resources/org/bitcoin/native/Mac/x86_64/'
            rename 'libsecp256k1.0.dylib', 'libsecp256k1.jnilib'
        }
        exec {
            workingDir 'secp256k1'
            commandLine './build_win.sh'
        }
        copy {
            from 'secp256k1/.libs/'
            include 'libsecp256k1-0.dll'
            into 'src/main/resources/org/bitcoin/native/Windows/x86_64/'
            rename 'libsecp256k1-0.dll', 'secp256k1.dll'
        }
    }
}

task cleanSecp256k1(group: 'secp256k1', description: 'removes libraries from main/resources folder') {
    doLast {
        delete 'src/main/resources/org/bitcoin/native/'
        delete 'secp256k1/.libs'
    }
}

// Aux

task generateJniHeaders(group: 'JNI', type: JavaCompile, description: 'generates JNI headers (.h) by java sources') {
    doFirst {
        exec {
            commandLine "mkdir", "-p", "build/generated/jni-tmp"
        }
    }
    classpath = sourceSets.main.compileClasspath
    destinationDir file("${buildDir}/generated/jni")
    source = sourceSets.main.java
    options.compilerArgs += [
            '-h', file("${buildDir}/generated/jni"),
            '-d', file("${buildDir}/generated/jni-tmp")
    ]
    doLast {
        delete file("${buildDir}/generated/jni-tmp")
    }
}

task replaceJniHeaders(group: 'JNI', description: 'Replaces JNI old headers with generated headers (for each library)') {
    dependsOn 'generateJniHeaders'
    doLast {
        copy {
            from "${buildDir}/generated/jni/org_bitcoin_NativeSecp256k1.h"
            into "${rootDir}/secp256k1/jni/"
        }
        copy {
            from "${buildDir}/generated/jni/org_bitcoin_Secp256k1Context.h"
            into "${rootDir}/secp256k1/jni/"
        }
    }
}

// Test Tasks

task testSecp256k1(type: Exec) {
    workingDir 'secp256k1'
    commandLine './test.sh'
}
